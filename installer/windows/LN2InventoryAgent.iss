; Inno Setup script for SnowFox.
; Build prerequisite: pyinstaller snowfox.spec

#define MyAppName "SnowFox"
#define MyAppPublisher "EamonFox"

#define MyAppVersion GetEnv("LN2_AGENT_VERSION")
#if MyAppVersion == ""
  #define MyAppVersion "1.3.16"
#endif

#define MyAppExeName "SnowFox-" + MyAppVersion + ".exe"

#define SourceDir "..\\..\\dist\\SnowFox"

#if !DirExists(SourceDir)
  #error "Missing dist/SnowFox. Build it first with: pyinstaller snowfox.spec"
#endif

#if !FileExists(SourceDir + "\\" + MyAppExeName)
  #error "Missing {#MyAppExeName} under dist/SnowFox."
#endif

[Setup]
AppId={{7C2D9B8B-A08F-4C69-A8E7-2A04A3010049}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={localappdata}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=..\..\dist\installer
OutputBaseFilename=SnowFox-Setup-{#MyAppVersion}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible
PrivilegesRequired=lowest
UninstallDisplayIcon={app}\{#MyAppExeName}
SetupIconFile=..\..\installer\windows\icon.ico
LicenseFile=..\..\LICENSE

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[CustomMessages]
english.LanguageLabel=Language:
english.ThemeLabel=Theme:
english.English=English
english.Chinese=中文 (简体)
english.Light=浅色
english.Dark=深色

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#SourceDir}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SourceDir}\_internal\migrate\*"; DestDir: "{app}\migrate"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SourceDir}\_internal\agent_skills\*"; DestDir: "{app}\agent_skills"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SourceDir}\_internal\migration_assets\*"; DestDir: "{app}\migration_assets"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\..\installer\windows\icon.ico"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\icon.ico"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\icon.ico"; Tasks: desktopicon

[Code]
var
  LanguagePage: TWizardPage;
  LanguageCombo: TComboBox;
  LanguageLabel: TLabel;
  ThemePage: TWizardPage;
  ThemeCombo: TComboBox;
  ThemeLabel: TLabel;

procedure InitializeWizard;
begin
  LanguagePage := CreateCustomPage(wpSelectDir, 'Language', 'Select your preferred language');
  
  LanguageLabel := TLabel.Create(LanguagePage);
  LanguageLabel.Parent := LanguagePage.Surface;
  LanguageLabel.Caption := 'Language:';
  LanguageLabel.Left := 0;
  LanguageLabel.Top := 10;

  LanguageCombo := TComboBox.Create(LanguagePage);
  LanguageCombo.Parent := LanguagePage.Surface;
  LanguageCombo.Left := 0;
  LanguageCombo.Top := 35;
  LanguageCombo.Width := 200;
  LanguageCombo.Items.Add('English');
  LanguageCombo.Items.Add('中文 (简体)');
  LanguageCombo.ItemIndex := 1;

  ThemePage := CreateCustomPage(LanguagePage.ID, 'Theme', 'Select your preferred theme');
  
  ThemeLabel := TLabel.Create(ThemePage);
  ThemeLabel.Parent := ThemePage.Surface;
  ThemeLabel.Caption := 'Theme:';
  ThemeLabel.Left := 0;
  ThemeLabel.Top := 10;

  ThemeCombo := TComboBox.Create(ThemePage);
  ThemeCombo.Parent := ThemePage.Surface;
  ThemeCombo.Left := 0;
  ThemeCombo.Top := 35;
  ThemeCombo.Width := 200;
  ThemeCombo.Items.Add('浅色 (Light)');
  ThemeCombo.Items.Add('深色 (Dark)');
  ThemeCombo.ItemIndex := 0;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  ConfigFile: string;
  ConfigDir: string;
  LangCode: string;
  ThemeCode: string;
  StringList: TStringList;
begin
  if CurStep = ssPostInstall then
  begin
    // Keep installer bootstrap path aligned with app_gui/gui_config.py default.
    // Target file: {app}\config\config.yaml
    ConfigDir := ExpandConstant('{app}\config');
    // Release structure:
    // - {app}\SnowFox-<version>.exe
    // - {app}\inventories\<dataset>\inventory.yaml
    // - {app}\config\config.yaml
    ForceDirectories(ExpandConstant('{app}\inventories'));
    ForceDirectories(ConfigDir);
    ConfigFile := ConfigDir + '\config.yaml';

    if LanguageCombo.ItemIndex = 0 then
      LangCode := 'en'
    else
      LangCode := 'zh-CN';

    if ThemeCombo.ItemIndex = 0 then
      ThemeCode := 'light'
    else
      ThemeCode := 'dark';

    // Preserve user config on reinstall/update.
    // New keys are backfilled by app_gui/gui_config.py when the app launches.
    if not FileExists(ConfigFile) then
    begin
      StringList := TStringList.Create;
      try
        StringList.Add('yaml_path: ""');
        StringList.Add('api_keys: {}');
        StringList.Add('language: "' + LangCode + '"');
        StringList.Add('theme: "' + ThemeCode + '"');
        StringList.Add('last_notified_release: "0.0.0"');
        StringList.Add('release_notes_preview: ""');
        StringList.Add('import_onboarding_seen: false');
        StringList.Add('ai:');
        StringList.Add('  provider: deepseek');
        StringList.Add('  model: null');
        StringList.Add('  max_steps: 120');
        StringList.Add('  thinking_enabled: true');
        StringList.Add('  custom_prompt: ""');
        StringList.SaveToFile(ConfigFile);
      finally
        StringList.Free;
      end;
    end;
  end;
end;

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#MyAppName}}"; Flags: nowait postinstall skipifsilent
